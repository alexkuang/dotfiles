# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# AUTO_TITLE in oh-my-zsh screws with tmux window renames
export DISABLE_AUTO_TITLE=true

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Some fanciness--instead of fg, just hit C-z again
fancy-ctrl-z () {
  if [[ $#BUFFER -eq 0 ]]; then
    BUFFER="fg"
    zle accept-line
  else
    zle push-input
    zle clear-screen
  fi
}
zle -N fancy-ctrl-z
bindkey '^z' fancy-ctrl-z

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# fzf
[ -f $HOMEBREW_PREFIX/opt/fzf/shell/key-bindings.zsh ] && source $HOMEBREW_PREFIX/opt/fzf/shell/key-bindings.zsh
export FZF_DEFAULT_COMMAND='fd'

export FZF_DEFAULT_OPTS="--exact --extended --preview '[[ \$(file --mime {}) =~ binary ]] && echo {} is a binary file || highlight -O xterm256 -l {} || cat {} 2> /dev/null | head -500' --bind '?:toggle-preview'"
export FZF_CTRL_R_OPTS="--exact --extended --preview 'echo {}' --bind '?:toggle-preview' --preview-window 'right:wrap'"
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"

# support 256 colors
BASE16_SHELL=$HOME/.config/base16-shell/
[ -n "$PS1" ] && \
        [ -s "$BASE16_SHELL/profile_helper.sh" ] && \
                source "$BASE16_SHELL/profile_helper.sh"

export EDITOR=vim

export RIPGREP_CONFIG_PATH="$HOME/.ripgreprc"

# `iex` history
export ERL_AFLAGS="-kernel shell_history enabled"

# try out fish-like syntax highlighting
source $HOMEBREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# rust + solana
. "$HOME/.cargo/env"
export PATH="/Users/alexkuang/.local/share/solana/install/active_release/bin:$PATH"
